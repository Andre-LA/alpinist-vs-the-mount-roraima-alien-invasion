require 'math'
require 'game_state'

local Vec2 = @Math.Vec2

local ProjParams = @record{
  pos: Vec2,
  dir: Vec2,
  should_create: boolean
}

global Game.Level = @record{
  alpinist: Game.Entities.Alpinist,
  win_screen_ratio: Vec2,
  initialized: boolean,
  map_pos_x: integer,
  map_height: integer,
  proj_alpinist: ProjParams,
  projs_flying_disc: vector(ProjParams),
  spawn_timer: number,
}
local Level = Game.Level

function Level.new(game: *Game.State): Level
  return (@Level){
    win_screen_ratio = {
      game.screen_size.x / game.window_size.x,
      game.screen_size.y / game.window_size.y,
    },
    projs_flying_disc = {},
  }
end

function Level:init(game: *Game.State)
  check(not self.initialized, 'A initialized level should not be initialized again')

  local tile_size = Settings.general.tile_size

  self.map_pos_x = game.screen_size.x - tile_size * 3
  self.map_height = tile_size * game.resources.map.tilemap:get_height()

  self.alpinist = game:create_alpinist({
    game.screen_size.x - tile_size * 3 - game.resources.alpinist.spritesheet.grid.cell_size.width,
    32
  })

  game:create_map({self.map_pos_x, 0})
  game:create_map({self.map_pos_x, -self.map_height})

  self.initialized = true
end

function Level:update(game: *Game.State)
  check(self.initialized, 'Level must be initialized before using this method')

  if self.proj_alpinist.should_create then
    game:create_projectile(
      self.proj_alpinist.pos, self.proj_alpinist.dir,
      Game.Entities.Projectile.Speed.Alpinist, Settings.IntersectableMasks.ProjAlpinist
    )
  end

  for i = 0, < #self.projs_flying_disc do
    game:create_projectile(
      self.projs_flying_disc[i].pos, self.projs_flying_disc[i].dir,
      Game.Entities.Projectile.Speed.Enemy, Settings.IntersectableMasks.ProjEnemy
    )
  end

  self.proj_alpinist.should_create = false
  self.projs_flying_disc:clear()

  self.spawn_timer = self.spawn_timer + game.nene.delta_time

  if self.spawn_timer >= 1 then
    local enemy_w: number, enemy_h: number = game.resources.flying_disc.spritesheet.grid.cell_size.width,
                                             game.resources.flying_disc.spritesheet.grid.cell_size.height

    game:create_flying_disc({-enemy_w, math.random(0, game.screen_size.y - enemy_h)}, self.alpinist.position_id)
    self.spawn_timer = 0
  end
end

function Level:alpinist_shoot(pos: Vec2, dir: Vec2)
  check(self.initialized, 'Level must be initialized before using this method')
  self.proj_alpinist = {=pos, =dir, true}
end

function Level:flying_disc_shoot(pos: Vec2, dir: Vec2)
  check(self.initialized, 'Level must be initialized before using this method')
  self.projs_flying_disc:push({=pos, =dir, true})
end

function Level:destroy()
  self.projs_flying_disc:destroy()
end
