require 'math'

require 'nene'
require 'rotor'
require 'irene.makers'

require 'life'
require 'shooter'
require 'entities.flying_disc.controllable'
require 'entities.flying_disc.controller'

local Vec2 = @Math.Vec2

global Game.Entities.FlyingDisc = @MakeEntity('flying_disc',
  Game.Comps.Life,
  Game.Comps.Shooter,
  Game.Comps.FlyingDiscControllable,

  Irene.Comps.Target,
  Irene.Comps.Follower,

  Irene.Comps.Position,
  Irene.Comps.Velocity,

  Irene.Comps.Intersectable,
  Irene.Comps.Intersections,
  Irene.Comps.Collider,

  Irene.Comps.TextureComp,
  Irene.Comps.Sprite,
  Irene.Comps.Painter
)
local FlyingDisc = Game.Entities.FlyingDisc

local function decrease_life_points(entity_id: GenerationalIndex): *Game.Comps.Life
  local life_opt = Game.Comps.Life.get_entry_from_entity_id(entity_id)
  if life_opt.filled then
    local life = life_opt:get()
    life.points = life.points - 1

    if life.points == 0 then
      Game.sfx_emitter.enemy_explosion = true
    else
      Game.sfx_emitter.enemy_hit = true
    end

    return life
  else
    return nilptr
  end
end

local function destroy_flying_disc(controllable: *Game.Comps.FlyingDiscControllable, entity_id: GenerationalIndex)
  local controllable_opt = Game.Comps.FlyingDiscControllable.get_entry_from_entity_id(entity_id)

  Irene.storages.entity:remove_entry(entity_id, Entity.destroy)
  Irene.storages.intersections:remove_entry(controllable.ids.intersections, Irene.Comps.Intersections.destroy)

  ##[[
  local should_not_autoremove = {
    entity_id = true,
    self_id = true,
    flying_disc_id = true,
    flying_disc_controllable_id = true,
    intersections_id = true,
    texture_comp_id = true,
  }
  ]]

  ## for i, field in ipairs(FlyingDisc.value.fields) do
    ## if not should_not_autoremove[field.name] then
      ## local actual_fname = field.name:sub(1, -4)
      Irene.storages.#|actual_fname|#:remove_entry(controllable.ids.#|actual_fname|#)
    ## end
  ## end

  Irene.storages.flying_disc_controllable:remove_entry(controllable.ids.flying_disc_controllable)
end

function FlyingDisc.create(
  nene: *Nene,
  pos: Vec2,
  flying_disc_texture: Texture,
  spritesheet: SpriteSheet,
  alpinist_pos_id: GenerationalIndex
): (FlyingDisc, *Entity)

  local gravity_acceleration <const> = Game.static_settings.gravity_acceleration
  local tile_size <const> = Game.static_settings.tile_size

  local shoot_interval <comptime> = 2500

  local flying_disc, flying_disc_ent = FlyingDisc.new{
    life = {
      points = 2,
    },

    shooter = {
      last_shoot = math.max(nene.current_time - (shoot_interval - 1000), 0),
    },

    flying_disc_controllable = {
      min_distance = tile_size * 7,
      =shoot_interval,
    },

    target = {
      pos_target_id = alpinist_pos_id,
    },

    follower = {
      speed = tile_size / 2,
    },

    position = {
      local_position = pos,
    },

    velocity = {
      active = true,
      max_velocity = tile_size * 12,
    },

    intersectable = {
      mask = Game.IntersectableMasks.Enemy,
    },

    intersections = {
      on_enter_callback = function(
          self_intersectable: *Irene.Comps.Intersectable,
          self_intersections: *Irene.Comps.Intersections,
          other_intersectable: *Irene.Comps.Intersectable
        )
          if other_intersectable.mask == Game.IntersectableMasks.ProjAlpinist then
            Game.Entities.Projectile.destroy_projectile(other_intersectable.entity_id)

            local life = decrease_life_points(self_intersectable.entity_id)

            local controllable_opt = Game.Comps.FlyingDiscControllable.get_entry_from_entity_id(self_intersectable.entity_id)
            if controllable_opt.filled then
              local controllable = controllable_opt:get()

              if life and life.points == 0 then
                destroy_flying_disc(controllable, self_intersectable.entity_id)
              end
            end
          end
        end
    },

    collider = {
      tolerance = 2,
      mask = Game.IntersectableMasks.SurfaceTile,
    },

    texture_comp = {
      texture = flying_disc_texture
    },

    sprite = {
      =spritesheet,
      layer = Game.SpriteLayer.Enemy,
    },

    painter = {
      color = Color.Palette.white,
      active = true,
    },
  }

  local intersectable_vertices: [2]Vec2 = { {}, { 20, 26} }
  local intersectable = flying_disc:get_component(@Irene.Comps.Intersectable)
  intersectable:init(pos, &intersectable_vertices)

  local controllable = flying_disc:get_component(@Game.Comps.FlyingDiscControllable)

  ##[[
  local ignore = {
    entity_id = true,
    self_id = true,
    flying_disc_id = true,
    texture_comp_id = true,
  }
  ]]

  ## for i, field in ipairs(FlyingDisc.value.fields) do
    ## if not ignore[field.name] then
      controllable.ids.#|field.name:sub(1, -4)|# = flying_disc.#|field.name|#
    ## end
  ## end

  return flying_disc, flying_disc_ent
end
