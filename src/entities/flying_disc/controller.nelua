require 'math'
require 'nene'
require 'rotor'
require 'rotor-quick.makers'

local RQ_Comps, GM_Comps = RotorQuick.Components, Game.Components

global Game.Systems.FlyingDiscController = @MakeSystem('FlyingDiscController', @record{
  flying_disc_controllable: GM_Comps.FlyingDiscControllable,
  shooter: GM_Comps.Shooter,
  target: RQ_Comps.Target,
  follower: RQ_Comps.Follower,
  position: RQ_Comps.Position,
  velocity: RQ_Comps.Velocity,
})

local FlyingDiscController = Game.Systems.FlyingDiscController

function FlyingDiscController:run(
  nene_state: *Nene.CoreState,
  wall_pos_x: number,
  proj_instantiator_callback: function(pos: Nene.Math.Vec2, dir: Nene.Math.Vec2)
)
  for _, components in self:iterate_components() do
    local controllable = components.flying_disc_controllable
    local shooter = components.shooter
    local target = components.target
    local follower = components.follower
    local position = components.position
    local velocity = components.velocity

    -- flying movement
    local fd_gl_pos = position:get_global_position()
    local wall_distance = wall_pos_x - fd_gl_pos.x
    local alpinist_distance = target:get_distance(fd_gl_pos)

    local is_alpinist_close = alpinist_distance < controllable.min_distance
    local is_wall_close = wall_distance < controllable.min_distance
    local is_right_of_wall = fd_gl_pos.x > wall_pos_x

    local dir_to_alpinist = target:get_direction(fd_gl_pos)
    local dir: Nene.Math.Vec2 = dir_to_alpinist

    if is_alpinist_close or is_wall_close then
      local neg_mul = is_alpinist_close and -1.3 or -2
      dir = { neg_mul, 0 }
    end

    local speed_abs = math.abs(follower.speed)
    follower.speed = is_alpinist_close and -speed_abs or speed_abs

    if is_alpinist_close or is_wall_close or is_right_of_wall then
      velocity:add_velocity(dir * (speed_abs / 6) * nene_state.deltatime)
    end

    velocity.final_velocity = RQ_Comps.Velocity.get_limited_velocity(velocity.final_velocity, velocity.max_velocity * nene_state.deltatime)

    -- shooting
    local current_time = nene_state:get_ms_time()
    local time_diff = current_time - shooter.last_shoot

    if time_diff >= controllable.shoot_interval then
      shooter.last_shoot = current_time

      local proj_pos = position:get_global_position() + (@Nene.Math.Vec2){24, 0}
      proj_instantiator_callback(proj_pos, dir_to_alpinist)
    end
  end
end
